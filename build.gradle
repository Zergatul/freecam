buildscript {
    repositories {
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.spongepowered.org/maven' }
        maven { url 'https://maven.minecraftforge.net/' }
        mavenCentral()
    }

    dependencies {
        classpath 'com.github.sdxqw:ForgeGradle:f2c5bb338e'
        classpath 'com.github.sdxqw:MixinGradle:0.6-SNAPSHOT'
        classpath 'com.github.jengelman.gradle.plugins:shadow:4.0.4'
    }
}

apply plugin: 'java'
apply plugin: 'net.minecraftforge.gradle.forge'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'com.github.johnrengelman.shadow'

String modid = "freecam"
String tweaker = "com.zergatul.freecam.FMLTweaker"

compileJava {
    sourceCompatibility = targetCompatibility = 8
}

version "0.1.1"
group "io.github.sdxqw"

minecraft {
    version = "1.8.9-11.15.1.2318-1.8.9"
    runDir = 'run'
    mappings = "stable_22"
    def args = [
            "-Dfml.coreMods.load=${tweaker}",
            '-Dmixin.hotSwap=true',
            '-Dmixin.checks.interfaces=true',
            "-XX:-DisableExplicitGC"
    ]
    clientJvmArgs.addAll(args)
    serverJvmArgs.addAll(args)
    makeObfSourceJar = true
}

configurations {
    sdxqw
    implementation.extendsFrom(sdxqw)

    runtimeOnly{
        canBeResolved = true
    }
}

repositories {
    maven { url 'https://jitpack.io' }
    maven { url 'https://repo.spongepowered.org/repository/maven-public/' }
    maven { url 'https://maven.minecraftforge.net/' }
    mavenCentral()
}

dependencies {
    annotationProcessor('org.spongepowered:mixin:0.7.11-SNAPSHOT')
    sdxqw('com.github.TheNullicorn:Nedit:2.1.0')

    sdxqw('org.spongepowered:mixin:0.7.11-SNAPSHOT') {
        exclude module: 'gson'
        exclude module: 'commons-io'
        exclude module: 'guava'
    }
}

reobf {
    shadowJar {
        classpath = sourceSets.main.compileClasspath
    }
}

mixin {
    defaultObfuscationEnv notch
    add sourceSets.main, "mixins.${modid}.refmap.json"
}

shadowJar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    configurations = [project.configurations.sdxqw]
    classifier ""
    exclude "LICENSE.txt", "dummyThing"
}

jar {
    manifest.attributes(
            'FMLCorePluginContainsFMLMod': true,
            'ForceLoadAsMod': true,
            'TweakClass': 'org.spongepowered.asm.launch.MixinTweaker',
            'MixinConfigs': "mixins.${modid}.json",
            'FMLCorePlugin': "${tweaker}"
    )
}

tasks.reobfJar.dependsOn(tasks.shadowJar)

processResources {
    inputs.property "version", project.version
    inputs.property "mcversion", project.minecraft.version

    from(sourceSets.main.resources.srcDirs) {
        include 'mcmod.info'

        expand 'version': project.version, 'mcversion': project.minecraft.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude 'mcmod.info'
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier "sources"
    from sourceSets.main.allSource
}

artifacts {
    archives jar
    archives shadowJar
    archives sourcesJar
}